//====================================================================
//ステージトラップスクリプト
//[10155][アルッテ・懺悔部屋]
//====================================================================

#ScriptTable

#ISABELLA	2
#MAURICE	3
#PENGUIN	10

#TARAI		11	//イベントでモーリスに落ちるタライ
#DUMMY		12

//=====================================================================
//=== 初期化処理 ======================================================
//=====================================================================
INIT
{
//  set_chr  no typ     tip         x     y   z   	 mot     向 状  000+++++++010+++++++020+++++++030+++++++040+++++++050+++++++060+++++++070+++++++080+++++++090+++++++100+++++++110+++++++120+++++++130+++++++140+++++++150+++++++160+++++++170+++++++180+++++++190+++++++200+++++++210+++++++220+++++++230+++++++240+++++++250++
//■エントリー
	set_chr( 90,990,	-1,-1,  -6600, -450,  0,  	 90,      0, 0,";")	//教会仮アルッテの街
	set_chr( 91,990,	-1,-1,  -6625, -450,  0,  	 90, 070202, 6,"K1_10150;")	//教会 行き
	
	
	//──────────────────────────────
	//▼⑬エンディング直前
	//──────────────────────────────
	if( F6_05_EndSixth )
	{
		set_chr( PENGUIN,81,	-1,-1, -7130,-580,0,	2,0,	20,"<TK_PENGUIN>")	//　呼び込みペンギン
		F_set_chr(PENGUIN,CF_NO_MOVE)
	}
	else
	//──────────────────────────────
	//▼⑪クリスタルバレーを起動することにした（ラグナ・ルゥ（・オデッサ））
	//──────────────────────────────
	if( F5_00_HearLunar && !F6_00_GoToMeru)
	{
		set_chr( ISABELLA,47,-1,-1, -7000,-800,0,	 52,0,	20,"<TK_ISABELLA>")	//懺悔室（現在仮）　イザベラ
		F_set_chr(ISABELLA,CF_NO_HEIGHT)
		F_set_chr(ISABELLA,CF_NO_FALL)
		set_chr( MAURICE,54,-1,-1,	 -7000,-530,0,	 63,180, 20,"<TK_MAURICE>")	//懺悔室（現在仮　モーリス
		set_chr( DUMMY,749,-1,-1,	 -7000,-490,-40,	  0,  0,  0,";")	//懺悔室（現在仮　モーリス
		look(MAURICE,"Bone_Head",0,		0,0,20,			0,0,-20)

		F_set_chr(DUMMY,CF_NO_ZIMEN)
		F_set_chr(DUMMY,CF_NO_HEIGHT)
//		F_set_chr(DUMMY,CF_NO_CLIP)
//		F_set_chr(DUMMY,CF_NO_CLIP2)
		F_set_chr(DUMMY,CF_NO_CSP)

		//モーション
		MOT_SET(MAURICE,150,-1,577,577,-1,-1)		//モーリスが座ってる
		MOT(MAURICE,150,5)
		
		//懺悔室に入るとモーリスとイザベラの共通会話が自動起動
		if(!FT_11_TalkIsabella)
		{
			EV("EV_TalkIsaMau")
		}
		F_set_chr(MAURICE,CF_NO_MOVE)
		F_set_chr(ISABELLA,CF_NO_MOVE)
	}
}

//====================================================================
//=== イベント処理 ===================================================
//====================================================================
//──────────────────────────────
//▼⑪イザベラとモーリスの会話
//──────────────────────────────
//　　※初回イベント。
//　　　懺悔室に入ると自動起動する。
//　　カメラスクロールで懺悔室を映し、ラグナたちは表示しない。
//──────────────────────────────
EV_TalkIsaMau
{
//――――――――――――――――――――――――――
//	初期化処理
//――――――――――――――――――――――――――
	fade_in(100,0,0)
	TK_begin()
	wait_fade()
	
	//カメラセット
	CAM_move(-6871,562,134,0,0)
	CAM(1110,105,-25,25,138,0,0)
	
	chr_pos(MAURICE, -7000,-530,0,	180,150)
	chr_pos(ISABELLA,-7000,-800,0,0,52)
	
	KAO(ISABELLA, "1", "1", "1")	
	KAO(MAURICE,"1", "1", "1")	

	//ラグナたちを非表示に
	color(RAGNA,-1)
	color(PARTNER,-1)
	color(PET,-1)

	//フェードイン----------------------
	fade_in(0,30,0)
	wait_fade()
//――――――――――――――――――――――――――
//	イベント開始
//――――――――――――――――――――――――――

	CAM_move(-6790,850,134,100,0)
	CAM(-290,76,-25,25,143,100,0)
	
	wait_CAM_move()

	
	MES(MAURICE,"そ、その\n娘との接し方が分からなくてね。",0)
	MES(MAURICE,"反抗期っていうのかな。\n最近なんて、食事時に尋ねてみても\n何も話してくれないんだよね。",0)
	MES(MAURICE,"もっと遊んでやらなくちゃと\n思ったりもするんだけど、\nなぜだか取り合ってくれないし……",0)
	MES(MAURICE,"はは、その……\n僕にも仕事があるしね。",0)
	MES_close(MAURICE)

//　　シスター、窓の向こうでタバコをふかす。
	MOT(ISABELLA,68,15)
	wait(60)
	MOT(ISABELLA,52,15)

//	name("イザベラ")
	MES(ISABELLA,"……で？",0)
	MES_close(ISABELLA)

//	name2(MAURICE)
	look(MAURICE,"Bone_Head",0,		0,2,3,			0,0,0)
	MES(MAURICE,"年頃の娘は難しいよね、\nな、なんてね……",0)
	MES_close(MAURICE)


//　　上からたらいが落ちてきてモーリスに当たり、派手な音を立てる。
//	MES(MAURICE,"C4◆上からたらいが落ちてきてモーリスに当たり、\n派手な音を立てる",0)
//	MES_close(MAURICE)
	EFF_chr(06900,DUMMY,0,100,0,"Bone_Head")
	wait(5)
	SE(1183,MAURICE)
	look(MAURICE,"Bone_Head",0,		0,2,10,			0,0,-60)
	wait(50)
	SE(1184,MAURICE)
	wait(70)

//	name("イザベラ")
	MES(ISABELLA,"フゥ……\nまずはその弱音癖を直すことね。",0)
	MES(ISABELLA,"失格よ。\n初めからやり直しなさい。",0)
	MES_close(ISABELLA)

//	name2(MAURICE)
	look(MAURICE,"Bone_Head",0,		0,2,2,			0,0,-40)
	MES(MAURICE,"は、はいぃ……",0)
	MES_close(MAURICE)
	wait_MES(MAURICE)
	
//　　クロスフェードで、懺悔室に入ったところからラグナたちに操作許可。
//――――――――――――――――――――――――――
//	イベント終了処理
//――――――――――――――――――――――――――
	cross_fade(30)
	
	//★⑪イザベラ・モーリス共通会話
	F_set(FT_11_TalkIsabella)
	//★イザベラ
	F_set_note(ISABELLA,-1)
	//★モーリス
	F_set_note(MAURICE,-1)

	//ラグナたちを表示する
	color(RAGNA,1)
	color(PARTNER,1)
	color(PET,1)
	look(MAURICE,"Bone_Head",0,		0,0,20,			0,0,-20)
	
	TK_end()
	//カメラを初期設定に
	CAM_return( 0 )
	
//	プレイヤー操作許可
//――――――――――――――――――――――――――

}

#EndScriptTable
//====================================================================

